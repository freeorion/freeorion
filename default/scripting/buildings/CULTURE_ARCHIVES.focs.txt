BuildingType
    name = "BLD_CULTURE_ARCHIVES"
    description = "BLD_CULTURE_ARCHIVES_DESC"
    buildcost = 200 * [[BUILDING_COST_MULTIPLIER]]
    buildtime = 1
    tags = "ANTIQUATED"
    location = Described description = "NOWHERE" condition = Not All
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
                Not Population high = 0
                OwnedBy empire = Source.Owner
                Happiness low = (NamedReal name = "BLD_CULTURE_ARCHIVES_MIN_STABILITY" value = 5)
            ]
            priority = [[TARGET_AFTER_2ND_SCALING_PRIORITY]]
            effects = [
                SetTargetResearch value = Value
                    + (NamedReal name = "BLD_CULTURE_ARCHIVES_TARGET_RESEARCH_FLAT" value = 5)
                SetTargetIndustry value = Value + Target.Population
                    * (NamedReal name = "BLD_CULTURE_ARCHIVES_TARGET_INDUSTRY_PERPOP"
                                 value = 2.5 * [[INDUSTRY_PER_POP]])
            ]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            activation = Not Unowned
            stackinggroup = "CULTURAL_ARCHIVES_POLICY_SLOT_STACK"
            effects = SetEmpireMeter empire = Source.Owner
                                     meter = "SOCIAL_CATEGORY_NUM_POLICY_SLOTS" value = Value + 1

    ]
    icon = "icons/building/archive.png"

#include "/scripting/macros/priorities.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/buildings/buildings.macros"
