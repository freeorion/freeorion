// sets stability (happiness) on planets in empire that have the building this macro effect is added to, if those planets' species like or dislike the building

STABILITY_PER_LIKED_BUILDING_ON_PLANET
'''4.0'''

// Policy Liberty doubles de building dislike effect, policy Conformance halves it, macro is in common/opinion.macros
STABILITY_PER_DISLIKED_BUILDING_ON_PLANET
'''4.0 * [[POLICY_DISLIKE_SCALING]]'''

STABILITY_PER_LIKED_BUILDING_IN_SYSTEM
'''1.0'''

STABILITY_PER_DISLIKED_BUILDING_IN_SYSTEM
'''1.0 * [[POLICY_DISLIKE_SCALING]]'''

SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS
'''
        // species like building on the same planet
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Population low = 0.001
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding > 0)
            ]
            accountinglabel = "LIKES_BUILDING_LABEL"
            effects = SetTargetHappiness value = Value + [[STABILITY_PER_LIKED_BUILDING_ON_PLANET]]

        // species dislike building on the same planet
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Population low = 0.001
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding < 0)
            ]
            accountinglabel = "DISLIKES_BUILDING_LABEL"
            effects = SetTargetHappiness value = Value - [[STABILITY_PER_DISLIKED_BUILDING_ON_PLANET]]
                
        // species like building in the same system
        EffectsGroup
            scope = And [
                Planet
                InSystem id = Source.SystemID
                Population low = 0.001
                Not Object id = Source.PlanetID // but not on same planet, which is covered by above case
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding > 0)
            ]
            accountinglabel = "LIKES_BUILDING_LABEL"
            effects = SetTargetHappiness value = Value + [[STABILITY_PER_LIKED_BUILDING_IN_SYSTEM]]

        // species dislike building in the same system
        EffectsGroup
            scope = And [
                Planet
                InSystem id = Source.SystemID
                Population low = 0.001
                Not Object id = Source.PlanetID // but not on same planet, which is covered by above case
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding < 0)
            ]
            accountinglabel = "DISLIKES_BUILDING_LABEL"
            effects = SetTargetHappiness value = Value - [[STABILITY_PER_DISLIKED_BUILDING_IN_SYSTEM]]

        // species like building in empire
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not InSystem id = Source.SystemID
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding > 0)
            ]
            accountinglabel = "LIKES_BUILDING_LABEL"
            effects =
                SetTargetHappiness value = Value + ((NamedReal name = "BUILDING_LIKE_EMPIRE_SQRT_SCALING" value = 0.5) * 
                    max(1.0,
                        Statistic Count condition = And [
                            Building
                            Building name = ThisBuilding
                            OwnedBy empire = Source.Owner
                            Not InSystem id = Target.SystemID
                        ]
                    )^(-0.5)
                )
 
        // species dislike building in empire
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not InSystem id = Source.SystemID
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisBuilding < 0)
            ]
            accountinglabel = "DISLIKES_BUILDING_LABEL"
            effects =
                SetTargetHappiness value = Value - ((NamedRealLookup name = "BUILDING_LIKE_EMPIRE_SQRT_SCALING") * 
                    max(1.0,
                        Statistic Count condition = And [
                            Building
                            Building name = ThisBuilding
                            OwnedBy empire = Source.Owner
                            Not InSystem id = Target.SystemID
                        ]
                    )^(-0.5)
                ) * [[POLICY_DISLIKE_SCALING]]
'''

DO_STARLANE_BORE
'''AddStarlanes endpoint = MinimumNumberOf
       number = 1
       sortkey = DirectDistanceBetween object = Source.ID object = LocalCandidate.ID
       condition = And [
           System
           CanAddStarlanesTo condition = Source
       ]
'''

BORE_POSSIBLE
'''Number
       low = 1
       condition = And [
           System
           CanAddStarlanesTo condition = Source
       ]
'''

#include "/scripting/common/opinion.macros"
