BuildingType
    name = "BLD_NEUTRONIUM_FORGE"
    description = "BLD_NEUTRONIUM_FORGE_DESC"
    buildcost = 100 * [[BUILDING_COST_MULTIPLIER]]
    buildtime = 3
    tags = "ORBITAL"
    location = And [
        Planet
        OwnedBy empire = Source.Owner
        Not Contains Building name = "BLD_NEUTRONIUM_FORGE"
        Contains And [
            Building name = "BLD_SHIPYARD_BASE"
            OwnedBy empire = Source.Owner
        ]
        Number low = 1 high = 999 condition = And [
            OwnedBy empire = Source.Owner
            Or [
                Building name = "BLD_NEUTRONIUM_EXTRACTOR"
                Building name = "BLD_NEUTRONIUM_SYNTH"
            ]
        ]
        TargetPopulation low = 1
    ]
    enqueuelocation = [[ENQUEUE_BUILD_ONE_PER_PLANET]]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_BUILDING_STABILITY_EFFECTS]]
    ]
    icon = "icons/building/neutronium-forge.png"

#include "/scripting/macros/enqueue.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/buildings/buildings.macros"
#include "/scripting/macros/priorities.macros"
