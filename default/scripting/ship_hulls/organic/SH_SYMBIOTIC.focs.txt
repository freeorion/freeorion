Hull
    name = "SH_SYMBIOTIC"
    description = "SH_SYMBIOTIC_DESC"
    speed = 100
    fuel = 2
    NoDefaultFuelEffect
    stealth = 15
    structure = 10
    slots = [
        Slot type = External position = (0.30, 0.30)
        Slot type = External position = (0.45, 0.30)
        Slot type = Internal position = (0.40, 0.55)
        Slot type = Internal position = (0.55, 0.55)
    ]
    buildcost = 12 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_HULL_COST_MULTIPLIER]]
    buildtime = 4
    tags = [ "ORGANIC_HULL" "PEDIA_HULL_LINE_ORGANIC" "GOOD_FUEL_EFFICIENCY" ]
    location = And [
        Planet
        OwnedBy empire = Source.Owner
        Contains And [
            Building name = "BLD_SHIPYARD_ORG_ORB_INC"
            OwnedBy empire = Source.Owner
        ]
        Contains And [
            Building name = "BLD_SHIPYARD_BASE"
            OwnedBy empire = Source.Owner
        ]
    ]
    effectsgroups = [
        EffectsGroup
            scope = Source
            accountinglabel = "ORGANIC_GROWTH"
            effects = SetMaxStructure value = Value + ( min(Source.Age*0.2, 10) * [[SHIP_STRUCTURE_FACTOR]] )

        [[GOOD_FUEL_EFFICIENCY]]
        [[ADD_HULL_FUEL_TO_MAX_FUEL_METER]]
        [[LIVING_HULL_EFFECTS_GROUPS]]
        [[SCAVANGE_FUEL_UNOWNED]]
        [[UNOWNED_GOOD_VISION]]
        [[UNOWNED_MOVE]]
    ]
    icon = "icons/ship_hulls/symbiotic_hull_small.png"
    graphic = "hulls_design/symbiotic_hull.png"

#include "organic.macros"

#include "../ship_hulls.macros"

#include "/scripting/macros/upkeep.macros"
