Hull
    name = "SH_PROTOPLASMIC"
    description = "SH_PROTOPLASMIC_DESC"
    speed = 100
    fuel = 2
    NoDefaultFuelEffect
    stealth = 25
    structure = 5
    NamedReal name = "SH_PROTOPLASMIC_ORGANIC_GROWTH_MAX" value = (25 * [[SHIP_STRUCTURE_FACTOR]])
    NamedReal name = "SH_PROTOPLASMIC_ORGANIC_GROWTH_PERTURN" value = (0.5 * [[SHIP_STRUCTURE_FACTOR]])
    slots = [
        Slot type = External position = (0.75, 0.40)
        Slot type = External position = (0.65, 0.65)
        Slot type = Internal position = (0.55, 0.30)
        Slot type = Internal position = (0.45, 0.525)
        Slot type = Internal position = (0.35, 0.75)
    ]
    buildcost = 15 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_HULL_COST_MULTIPLIER]]
    buildtime = 5
    tags = [ "ORGANIC_HULL" "PEDIA_HULL_LINE_ORGANIC" "GOOD_FUEL_EFFICIENCY" ]
    location = And [
        Planet
        OwnedBy empire = Source.Owner
        Contains And [
            Building name = "BLD_SHIPYARD_ORG_CELL_GRO_CHAMB"
            OwnedBy empire = Source.Owner
        ]
        Contains And [
            Building name = "BLD_SHIPYARD_ORG_ORB_INC"
            OwnedBy empire = Source.Owner
        ]
        Contains And [
            Building name = "BLD_SHIPYARD_BASE"
            OwnedBy empire = Source.Owner
        ]
    ]
    effectsgroups = [
        EffectsGroup
            scope = Source
            accountinglabel = "ORGANIC_GROWTH"
            effects = SetMaxStructure value = Value
                + min(
                (Source.Age * (NamedRealLookup name = "SH_PROTOPLASMIC_ORGANIC_GROWTH_PERTURN")),
                (NamedRealLookup name = "SH_PROTOPLASMIC_ORGANIC_GROWTH_MAX")
                )

        [[GOOD_FUEL_EFFICIENCY]]
        [[ADD_HULL_FUEL_TO_MAX_FUEL_METER]]
        [[LIVING_HULL_EFFECTS_GROUPS]]
        [[SCAVANGE_FUEL_UNOWNED]]
        [[UNOWNED_GOOD_VISION]]
        [[UNOWNED_MOVE]]
    ]
    icon = "icons/ship_hulls/protoplasmic_hull_small.png"
    graphic = "hulls_design/protoplasmic_hull.png"

#include "organic.macros"

#include "../ship_hulls.macros"

#include "/scripting/common/upkeep.macros"
