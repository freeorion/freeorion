//macros used for organic ships
LIVING_HULL_EFFECTS_GROUPS
'''EffectsGroup
    scope = Source
    effects = [
        SetStructure value = Value + (NamedRealLookup name = "SHP_LIVING_HULL_REPAIR")
        SetDetection value = Value + 40
    ]

    [[LIVING_HULL_BASE_FUEL_REGEN]]
'''

LIVING_HULL_BASE_FUEL_REGEN
'''EffectsGroup
    description = "LIVING_HULL_BASE_FUEL_REGEN_DESC"
    scope = Source
    activation = And [
        Turn low = LocalCandidate.ArrivedOnTurn + 1
        Stationary
        (Source.Fuel < Source.MaxFuel)
    ]
    stackinggroup = "BASE_FUEL_REGEN"
    accountinglabel = "BASE_FUEL_REGEN_LABEL"
    priority = [[EARLY_AFTER_ALL_TARGET_MAX_METERS_PRIORITY]]
    effects = SetFuel value = min(Target.MaxFuel, Value + (NamedRealLookup name = "SHP_LIVING_HULL_FUEL_REGEN"))

   [[REFUEL_MESSAGE(0.3)]]
'''

LIVING_HULL_GROWTH_EFFECTS_GROUPS
'''EffectsGroup
    scope = Source
    accountinglabel = "ORGANIC_GROWTH"
    effects = SetMaxStructure value = Value + min(Source.Age * (NamedRealLookup name = "@1@_GROWTH_PER_TURN"), NamedIntegerLookup name = "@1@_GROWTH_STRUCTURE")
'''


#include "/scripting/macros/priorities.macros"

#include "/scripting/macros/misc.macros"
