Hull
    name = "SH_KRAKEN_1_BODY"
    description = "SH_KRAKEN_1_BODY_DESC"
    speed = 40
    fuel = 4
    NoDefaultFuelEffect
    stealth = 5
    structure = 20
    slots = [
        Slot type = External position = (0.25, 0.70)
        Slot type = Internal position = (0.55, 0.45)
    ]
    buildcost = 1
    buildtime = 1
    Unproducible
    tags = [ "PEDIA_HULL_MONSTER_KRAKEN" ]
    location = All
    effectsgroups = [
        EffectsGroup    // remove self and recreate on first turn, so that it starts with age 0
            scope = Source
            activation = Turn high = 1
            effects = [
                CreateShip designname = "SM_KRAKEN_1"
                Destroy
            ]

        EffectsGroup
            scope = NumberOf number = 2 condition = And [
                Planet
                Planet type = GasGiant
                Not WithinStarlaneJumps jumps = 3 condition = And [
                    System
                    Contains And [
                        Planet
                        OwnedBy affiliation = AnyEmpire
                    ]
                ]
            ]
            activation = Turn high = 2
            stackinggroup = "GAME_START_MOD_STACK"
            effects = AddSpecial name = "KRAKEN_NEST_SPECIAL"

        [[GAS_GIANT_STEALTH_BONUS]]
        [[MONSTER_MOVE_ALWAYS]]
        [[INFINITE_FUEL]]
        [[ADD_HULL_FUEL_TO_MAX_FUEL_METER]]
        [[MONSTER_SHIELD_REGENERATION]]

        EffectsGroup
            scope = Source
            activation = And [
                InSystem
                ContainedBy And [
                    Object id = Source.SystemID
                    Contains Planet type = GasGiant
                ]
                Turn low = max(30, LocalCandidate.LastTurnActiveInBattle + 1)
                //triple the chance of maturing when in a system with Krill
                Random probability = (LocalCandidate.Age*0.01 - 0.1) * 3^(Statistic If condition = And [
                    Source
                    InSystem
                    ContainedBy And [
                        Object id = Source.SystemID
                        Contains And [
                            InSystem id = Source.SystemID
                            Ship
                            Or [
                                Design name = "SM_KRILL_1"
                                Design name = "SM_KRILL_2"
                                Design name = "SM_KRILL_3"
                                Design name = "SM_KRILL_4"
                            ]
                        ]
                    ]
                ])
            ]
            effects = [
                CreateShip designname = "SM_KRAKEN_2" empire = Source.Owner name = Source.Name
                Destroy
                GenerateSitRepMessage
                    message = "EFFECT_TAME_MONSTER_MATURED"
                    label = "EFFECT_TAME_MONSTER_MATURED_LABEL"
                    icon = "icons/monsters/kraken-2.png"
                    parameters = [
                        tag = "system" data = Source.SystemID
                        tag = "predefinedshipdesign" data = "SM_KRAKEN_2"
                    ]
                    empire = Source.Owner
            ]
        [[MODERATE_VISION]]
    ]
    icon = ""
    graphic = "icons/monsters/kraken-1.png"

#include "monster.macros"

#include "../ship_hulls.macros"
