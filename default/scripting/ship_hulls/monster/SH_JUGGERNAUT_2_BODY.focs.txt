Hull
    name = "SH_JUGGERNAUT_2_BODY"
    description = "SH_JUGGERNAUT_2_BODY_DESC"
    speed = 25
    fuel = 2
    NoDefaultFuelEffect
    stealth = 5
    structure = 200
    slots = [
        Slot type = External position = (0.35, 0.70)
        Slot type = External position = (0.65, 0.75)
        Slot type = External position = (0.95, 0.65)
        Slot type = External position = (0.20, 0.50)
        Slot type = Internal position = (0.50, 0.30)
    ]
    buildcost = 1
    buildtime = 1
    Unproducible
    tags = [ "PEDIA_HULL_MONSTER_JUGGERNAUT" ]
    location = All
    effectsgroups = [
        [[MONSTER_MOVE_ALWAYS]]
        [[INFINITE_FUEL]]
        [[ADD_HULL_FUEL_TO_MAX_FUEL_METER]]
        [[MONSTER_SHIELD_REGENERATION]]

        EffectsGroup
            scope = Source
            activation = And [
                InSystem
                Turn low = max(60, Source.LastTurnActiveInBattle + 1)
                ContainedBy Contains Planet type = Asteroids
                Random probability = Source.Age*0.01 - 0.1
            ]
            effects = [
                CreateShip designname = "SM_JUGGERNAUT_3" empire = Source.Owner name = Source.Name
                Destroy
                GenerateSitRepMessage
                    message = "EFFECT_TAME_MONSTER_MATURED"
                    label = "EFFECT_TAME_MONSTER_MATURED_LABEL"
                    icon = "icons/monsters/juggernaut-3.png"
                    parameters = [
                        tag = "system" data = Source.SystemID
                        tag = "predefinedshipdesign" data = "SM_JUGGERNAUT_3"
                    ]
                    empire = Source.Owner
            ]
        
         EffectsGroup
            scope = Source
            effects = [
                SetMaxCapacity partname = "SR_JAWS" value = Value + (min(Source.Age*0.1, 8) * [[SHIP_WEAPON_DAMAGE_FACTOR]])
                SetCapacity partname = "SR_JAWS" value = Value + (min(Source.Age*0.1, 8) * [[SHIP_WEAPON_DAMAGE_FACTOR]])
            ]
        
        [[GOOD_VISION]]

    ]
    icon = ""
    graphic = "icons/monsters/juggernaut-2.png"

#include "monster.macros"

#include "../ship_hulls.macros"
#include "/scripting/macros/misc.macros"
