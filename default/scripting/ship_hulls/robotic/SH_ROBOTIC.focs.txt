Hull
    name = "SH_ROBOTIC"
    description = "SH_ROBOTIC_DESC"
    speed = 75
    fuel = 3
    stealth = 5
    structure = 25
    slots = [
        Slot type = External position = (0.25, 0.25)
        Slot type = External position = (0.40, 0.25)
        Slot type = External position = (0.55, 0.25)
        Slot type = External position = (0.70, 0.25)
        Slot type = Internal position = (0.50, 0.55)
    ]
    buildCost = 40 * [[FLEET_UPKEEP_MULTIPLICATOR]]
    buildTime = 2
    tags = [ "ROBOTIC_HULL" ]
    location = And [
        Contains And [
            Building name = "BLD_SHIPYARD_BASE"
            OwnedBy empire = Source.Owner
        ]
        Contains And [
            Building name = "BLD_SHIPYARD_ORBITAL_DRYDOCK"
            OwnedBy empire = Source.Owner
        ]
        OwnedBy empire = Source.Owner
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Source
                Or [
                    Not InSystem
                    And [
                        InSystem
                        Turn low = LocalCandidate.System.LastTurnBattleHere + 1
                    ]
                ]
                Structure high = LocalCandidate.MaxStructure - 0.001
            ]
            activation = Source
            effects = SetStructure value = Value + 2

        [[REGULAR_HULL_DETECTION]]
        [[SCAVANGE_FUEL_UNOWNED]]
        [[UNOWNED_GOOD_VISION]]
        [[UNOWNED_MOVE]]
    ]
    icon = "icons/ship_hulls/robotic_hull_small.png"
    graphic = "hulls_design/robotic_hull.png"

#include "robotic.macros"

#include "../ship_hulls.macros"

#include "/scripting/common/upkeep.macros"
