Policy
    name = "PLC_CENTRALIZATION"
    description = "PLC_CENTRALIZATION_DESC"
    short_description = "PLC_CENTRALIZATION_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = 1.0 * floor(5 + 0.25 * Statistic Sum value = LocalCandidate.Population
                                          condition = And [ Planet OwnedBy empire = Source.Owner ])
    exclusions = [ "PLC_CONFEDERATION" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
                Happiness low = 0
            ]
            effects = [
                SetTargetIndustry value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_INDUSTRY_PERPOP"
                                 value = 1.0 * [[INDUSTRY_PER_POP]])
                SetTargetResearch value = Value + Target.Population
                    * (NamedReal name = "PLC_CENTRALIZATION_TARGET_RESEARCH_PERPOP"
                                 value = 1.0 * [[RESEARCH_PER_POP]])
                SetTargetConstruction value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_TARGET_CONSTRUCTION_FLAT" value = 10)
                SetMaxSupply value = Value
                    + (NamedReal name = "PLC_CENTRALIZATION_CAPITAL_MAX_SUPPLY_FLAT" value = 1)
            ]

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not ResourceSupplyConnected empire = LocalCandidate.Owner condition = And [
                    Planet
                    OwnedBy empire = Source.Owner
                    Capital
                ]
            ]
            effects = SetTargetInfluence value = Value
                        + (NamedReal name = "PLC_CENTRALIZATION_TARGET_INFLUENCE_FLAT" value = -2)

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not Capital
            ]
            effects = [
                SetMaxSupply value = Value + (NamedReal name = "PLC_CENTRALIZATION_MAX_SUPPLY_FLAT" value = -1)
                SetMaxStockpile value = Value + (NamedReal name = "PLC_CENTRALIZATION_MAX_STOCKPILE_FLAT" value = -1)
            ]
    ]
    graphic = "icons/policies/centralization.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/policies/policies.macros"
