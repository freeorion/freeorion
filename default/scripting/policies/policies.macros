// sets stability (happiness) on planets in empire that has adopted the policy this macro effect is added to, if those planets' species like or dislike the policy

STABILITY_PER_LIKED_POLICY
'''2.0'''

// Policy Liberty doubles de dislike effect, policy Conformance halves it, macro is in common/opinion.macros
STABILITY_PER_DISLIKED_POLICY
'''2.0 * [[POLICY_DISLIKE_SCALING]]'''

SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS
'''
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisPolicy > 0)
            ]
            accountinglabel = "LIKES_POLICY_LABEL"
            effects =
                SetTargetHappiness value = Value + [[STABILITY_PER_LIKED_POLICY]]

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                (SpeciesContentOpinion species = LocalCandidate.Species name = ThisPolicy < 0)
            ]
            accountinglabel = "DISLIKES_POLICY_LABEL"
            effects =
                SetTargetHappiness value = Value - [[STABILITY_PER_DISLIKED_POLICY]]
'''

POPULATION_OWNED_BY_EMPIRE
'''(Statistic Sum value = LocalCandidate.Population condition = And [ Planet OwnedBy empire = Source.Owner ])'''

PLANETS_OWNED_BY_EMPIRE
'''(Statistic Count condition = And [ Planet Species OwnedBy empire = Source.Owner ])'''

SHIPS_OWNED_BY_EMPIRE
'''(Statistic Count condition = And [Ship OwnedBy empire = Source.Owner])'''

#include "/scripting/common/opinion.macros"
