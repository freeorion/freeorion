Policy
    name = "PLC_AUGMENTATION"
    description = "PLC_AUGMENTATION_DESC"
    short_description = "PLC_AUGMENTATION_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_RACIAL_PURITY" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Planet environment = [ Hostile ]
            ]
            priority = [[TARGET_POPULATION_BEFORE_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 2 * Target.HabitableSize

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Species
            ]
            priority = [[TARGET_EARLY_BEFORE_SCALING_PRIORITY]]
            effects = [
                SetMaxTroops value = Value + Target.Population
                                     * (NamedReal name = "CYBORG_MAX_TROOPS_PERPOP" value = 1.5 * [[TROOPS_PER_POP]])
            ]
    ]
    graphic = "icons/policies/social_augmentation.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/macros/priorities.macros"
