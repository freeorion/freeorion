Policy
    name = "PLC_CONTINUOUS_SCANNING"
    description = "PLC_CONTINUOUS_SCANNING_DESC"
    short_description = "PLC_CONTINUOUS_SCANNING_SHORT_DESC"
    category = "MILITARY_CATEGORY"
    adoptioncost = 5 + Statistic Count condition = And [Ship OwnedBy empire = Source.Owner]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Ship
                OwnedBy empire = Source.Owner
            ]
            effects = [
                SetDetection value = Value + 5
                SetStealth value = Value - 10
            ]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            effects = SetEmpireMeter
                empire = Source.Owner
                meter = "METER_DETECTION_STRENGTH"
                value = Value + min(20.0,
                    CurrentTurn - TurnPolicyAdopted empire = source.Owner
                                                    name = "PLC_CONTINUOUS_SCANNING"
                                   )
    ]
    graphic = "icons/policies/continuous_scanning.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/common/priorities.macros"
