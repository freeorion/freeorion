Policy
    name = "PLC_DIVERSITY"
    description = "PLC_DIVERSITY_DESC"
    short_description = "PLC_DIVERSITY_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = 1.0*floor(5 + 0.25 * Statistic Sum value = LocalCandidate.Population condition = And [ Planet OwnedBy empire = Source.Owner ])
    exclusions = [ "PLC_RACIAL_PURITY" ]
    unlock = Item type = Policy name = "PLC_ARTISAN_WORKSHOPS"
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]
    
        // research bonus for more species in empire, penalty when less than 4 species
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_RESEARCH"
                Happiness low = (NamedReal name = "PLC_DIVERSITY_MIN_STABILITY" value = 8)
            ]
            effects = [
                SetTargetResearch value = Value - 2 + (NamedReal name = "PLC_DIVERSITY_SCALING" value = 0.5) * Statistic CountUnique
                    value = LocalCandidate.Species
                    condition = And [
                        Planet
                        OwnedBy empire = Source.Owner
                        Population low = 0.001
                    ]
            ]
        
        // happiness bonus for more species in empire, penalty when less than 4 species
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
            ]
            effects = [
                SetTargetHappiness value = Value - 2 + NamedRealLookup name = "PLC_DIVERSITY_SCALING" * Statistic CountUnique
                    value = LocalCandidate.Species
                    condition = And [
                        Planet
                        OwnedBy empire = Source.Owner
                        Population low = 0.001
                    ]
            ]

        // influence bonus on capital for more species, penalty when less than 4 species
        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            effects = [
                SetTargetInfluence value = Value - 2 + NamedRealLookup name = "PLC_DIVERSITY_SCALING" * Statistic CountUnique
                    value = LocalCandidate.Species
                    condition = And [
                        Planet
                        OwnedBy empire = Source.Owner
                        Population low = 0.001
                    ]
            ]
    ]
    graphic = "icons/policies/diversity.png"

#include "/scripting/policies/policies.macros"
