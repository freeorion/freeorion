Policy
    name = "PLC_COLONIZATION"
    description = "PLC_COLONIZATION_DESC"
    short_description = "PLC_COLONIZATION_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_CENTRALIZATION" "PLC_METROPOLES" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        // Makes colony buildings and colony/outpost ship parts cheaper
        // See COLONIZATION_POLICY_MULTIPLIER macro in ../common/upkeep.macros

        EffectsGroup
            scope = And [
                Ship
                OwnedBy empire = Source.Owner
                DesignHasPartClass low = 1 class = Colony
                DesignHasPartClass low = 0 high = 0 class = ShortRange
                DesignHasPartClass low = 0 high = 0 class = FighterBay
                DesignHasPartClass low = 0 high = 0 class = FighterHangar
                DesignHasPartClass low = 0 high = 0 class = Troops
                DesignHasPartClass low = 0 high = 0 class = General
                DesignHasPartClass low = 0 high = 0 class = Bombard
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetMaxFuel value = Value + 1
    ]
    graphic = "icons/policies/social_colonization.png"

#include "/scripting/macros/priorities.macros"
#include "/scripting/policies/policies.macros"
