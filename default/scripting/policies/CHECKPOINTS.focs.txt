Policy
    name = "PLC_CHECKPOINTS"
    description = "PLC_CHECKPOINTS_DESC"
    short_description = "PLC_CHECKPOINTS_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = 5 + floor(Statistic Sum value = LocalCandidate.Population
                                           condition = And [ Planet OwnedBy empire = Source.Owner ])
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
            ]
            effects = [
                SetTargetInfluence value = Value 
                   + (NamedReal name = "PLC_CHECKPOINTS_TARGET_INFLUENCE_FLAT" value = -2)
                SetMaxSupply value = Value 
                   + (NamedReal name = "PLC_CHECKPOINTS_MAX_SUPPLY_FLAT" value = -1)
            ]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            effects = SetEmpireMeter
                empire = Source.Owner
                meter = "METER_DETECTION_STRENGTH"
                value = Value + min((NamedReal name = "PLC_CHECKPOINTS_DETECTION_STRENGTH_MAX" value = 20.0),
                                    CurrentTurn - TurnPolicyAdopted empire = Source.Owner name = "PLC_CHECKPOINTS")
    ]
    graphic = "icons/policies/checkpoints.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/common/priorities.macros"
