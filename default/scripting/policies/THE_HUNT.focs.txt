Policy
    name = "PLC_THE_HUNT"
    description = "PLC_THE_HUNT_DESC"
    short_description = "PLC_THE_HUNT_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_DIVERSITY" "PLC_NATIVE_APPROPRIATION" "PLC_LIBERTY" "PLC_VASSALIZATION" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]
        
        // planets owned by this empire that are in the same system as another planet that is unowned and has a different species on it can hunt the latter species
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                ContainedBy Contains And [
                    InSystem id = RootCandidate.SystemID
                    Planet
                    Unowned
                    Species
                    Not Species name = RootCandidate.Species
                    Not Species name = Source.Species
                    Shield high = 0
                    Defense high = 0                    
                ]
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = [
                SetTargetHappiness value = Value
                    + (NamedReal name = "PLC_THE_HUNT_HAPPINESS_FLAT" value = 10.0)
                SetTargetInfluence value = Value
                    + (NamedReal name = "PLC_THE_HUNT_INFLUENCE_FLAT" value = 5.0)
                SetMaxTroops value = Value
                    + (NamedReal name = "PLC_THE_HUNT_TROOPS_MAX_FLAT" value = 12.0)
            ]
    ]
    graphic = "icons/policies/social_hunt.png"

#include "/scripting/macros/base_prod.macros"
#include "/scripting/policies/policies.macros"
#include "/scripting/macros/priorities.macros"
