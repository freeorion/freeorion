Policy
    name = "PLC_PROPAGANDA" // Imperial Propaganda
    description = "PLC_PROPAGANDA_DESC"
    short_description = "PLC_PROPAGANDA_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = 8.0
    unlock = Item type = Policy name = "PLC_MARINE_RECRUITMENT"
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Capital
                OwnedBy empire = Source.Owner
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetTargetInfluence value = Value + (NamedReal name = "PROPAGANDA_INFLUENCE_FLAT" value = 4.0)

        EffectsGroup
            scope = And [
                Capital
                OwnedBy affiliation = AllyOf empire = Source.Owner
                Not EmpireHasAdoptedPolicy empire = LocalCandidate.Owner name = ThisPolicy
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetTargetInfluence value = Value + (NamedReal name = "PROPAGANDA_INFLUENCE_ALLIED_FLAT" value = 1.0)

        EffectsGroup
            scope = And [
                Capital
                OwnedBy affiliation = EnemyOf empire = Source.Owner
                Not EmpireHasAdoptedPolicy empire = LocalCandidate.Owner name = ThisPolicy
                Number low = 1 condition = And [
                    OwnedBy empire = Source.Owner
                    VisibleToEmpire empire = RootCandidate.Owner
                ]
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetTargetInfluence value = Value + (NamedReal name = "PROPAGANDA_INFLUENCE_ENEMY_FLAT" value = -1.0)
    ]
    graphic = "icons/policies/social_propaganda.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/macros/priorities.macros"
