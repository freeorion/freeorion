Policy
    name = "PLC_CONFEDERATION"
    description = "PLC_CONFEDERATION_DESC"
    short_description = "PLC_CONFEDERATION_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_CENTRALIZATION" ]
    unlock = [
        Item type = Policy name = "PLC_VASSALIZATION"
        Item type = Policy name = "PLC_FEUDALISM"
    ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        // compensates for species influence effect that reduces influence when not supply connected to capital
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not ResourceSupplyConnected empire = LocalCandidate.Owner condition = Source
            ]
            effects = SetTargetInfluence value = Value
                        + (NamedReal name = "PLC_CONFEDERATION_SUPPLY_DISCONNECTED_INFLUENCE_FLAT" value = [[SUPPLY_DISCONNECTED_INFLUENCE_MALUS]])

        // makes all planets less stable
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
            ]
            effects = [
                SetTargetHappiness value = Value 
                    + (NamedReal name = "PLC_CONFEDERATION_TARGET_HAPPINESS_FLAT" value = -1)
                SetMaxTroops value = Value
                    + (NamedReal name = "PLC_CONFEDERATION_MAX_TROOPS_FLAT" value = 5)
            ]

        // compensates for supply disconnection stability penalty
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Not ResourceSupplyConnected empire = Source.Owner condition = And [
                    Or [Capital Building name = "BLD_REGIONAL_ADMIN"]
                    OwnedBy empire = Source.Owner
                ]
                Not Capital
            ]
            effects = SetTargetHappiness value = Value + (NamedRealLookup name = "DISCONNECTED_FROM_CAPITAL_AND_REGIONAL_ADMIN_STABILITY_PENALTY")
    ]
    graphic = "icons/policies/confederation.png"

#include "/scripting/policies/policies.macros"
#include "/scripting/common/misc.macros"
