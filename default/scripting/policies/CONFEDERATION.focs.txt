Policy
    name = "PLC_CONFEDERATION"
    description = "PLC_CONFEDERATION_DESC"
    short_description = "PLC_CONFEDERATION_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = 1.0*floor(5 + 0.25 * Statistic Sum value = LocalCandidate.Population condition = And [ Planet OwnedBy empire = Source.Owner ])
    exclusions = [ "PLC_CENTRALIZATION" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        // compensates for species influence effect that reduces influence when not supply connected to capital
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
                Not ResourceSupplyConnected empire = LocalCandidate.Owner condition = Source
            ]
            priority = [[DEFAULT_PRIORITY]]
            effects = SetTargetInfluence value = Value +
                          (NamedReal name = "PLC_CONFEDERATION_TARGET_INFLUENCE_FLAT" value = 1)

        // makes all planets less stable
        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Population low = 0.001
            ]
            priority = [[VERY_EARLY_PRIORITY]]
            effects = [
                SetTargetHappiness value = Value + (NamedReal name = "PLC_CONFEDERATION_TARGET_HAPPINESS_FLAT" value = (-1))
                SetMaxTroops value = Value + (NamedReal name = "PLC_CONFEDERATION_MAX_TROOPS_FLAT" value = 5)
            ]
    ]
    graphic = "icons/policies/confederation.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/policies/policies.macros"
