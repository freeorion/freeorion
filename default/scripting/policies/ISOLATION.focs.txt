Policy
    name = "PLC_ISOLATION"
    description = "PLC_ISOLATION_DESC"
    short_description = "PLC_ISOLATION_SHORT_DESC"
    category = "SOCIAL_CATEGORY"
    adoptioncost = 1.0 * floor(2 + 2 * Statistic Count
                                           condition = And [ Planet Species OwnedBy empire = Source.Owner ])
    exclusions = [ "PLC_BOOTSTRAPPING" "PLC_CENTRALIZATION" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Planet
                Species
                OwnedBy empire = Source.Owner
            ]
            activation = Number low = 2 condition = And [
                Planet
                Species
                VisibleToEmpire empire = Source.Owner
            ]
            effects =
                SetTargetHappiness value = Value + min(
                    10.0, 
                    (Statistic Min
                        value = DirectDistanceBetween object = Target.SystemID object = LocalCandidate.SystemID
                        condition = And [
                            Planet
                            Species
                            VisibleToEmpire empire = Source.Owner
                            Not Target
                        ]
                    ) / (NamedReal name = "PLC_ISOLATION_THRESHOLD_DISTANCE" value = 200.0) * 10.0 - 10.0)

        EffectsGroup
            scope = And [
                Planet
                Species
                OwnedBy empire = Source.Owner
            ]
            activation = Number high = 1 condition = And [
                Planet
                Species
                VisibleToEmpire empire = Source.Owner
            ]
            effects = SetTargetHappiness value = Value + 10.0
        
        EffectsGroup
            scope = And [
                Planet
                Species
                OwnedBy empire = Source.Owner
            ]
            effects = SetStealth value = Value + (NamedReal name = "PLC_ISOLATION_STEALTH" value = 5.0)
    ]
    graphic = "icons/policies/isolation.png"

#include "/scripting/policies/policies.macros"
