Policy
    name = "PLC_TECHNOCRACY"
    description = "PLC_TECHNOCRACY_DESC"
    short_description = "PLC_TECHNOCRACY_SHORT_DESC"
    category = "ECONOMIC_CATEGORY"
    adoptioncost = floor(2.0 * [[POPULATION_OWNED_BY_EMPIRE]]^0.5)
    exclusions = [ "PLC_INDUSTRIALISM" "PLC_MODERATION" ]
    effectsgroups = [
        [[SPECIES_LIKES_OR_DISLIKES_POLICY_STABILITY_EFFECTS]]

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_RESEARCH"
                Population low = 0.001
                Happiness low = (NamedReal name = "PLC_TECHNOCRACY_MIN_STABILITY" value = 5)
            ]
            priority = [[TARGET_2ND_SCALING_PRIORITY]]
            effects =
                SetTargetResearch value = Value
                    + abs(Value * (NamedReal name = "PLC_TECHNOCRACY_TARGET_RESEARCH_PERCENT" value = 25) / 100)

        EffectsGroup
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_RESEARCH"
                Population low = 0.001
            ]
            effects =
                SetTargetHappiness value = Value
                    + (NamedReal name = "PLC_TECHNOCRACY_TARGET_HAPPINESS_FLAT" value = 2)
    ]
    graphic = "icons/policies/economic_technocracy.png"

#include "/scripting/macros/priorities.macros"
#include "/scripting/policies/policies.macros"
