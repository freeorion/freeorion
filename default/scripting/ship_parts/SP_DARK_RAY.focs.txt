Part
    name = "SP_DARK_RAY"
    description = "SP_DARK_RAY_DESC"
    class = Bombard
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]]
    buildtime = 3
    location = All
    effectsgroups = [
        EffectsGroup    // players can order dark ray used on enemies
            scope = And [
                Planet
                InSystem id = Source.SystemID
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                HasTag name = "PHOTOTROPHIC"
            ]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    icon = "icons/ship_parts/death-spore.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DARK_RAY"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use dark ray on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                HasTag name = "PHOTOTROPHIC"
            ]
            activation = And [
                            Unowned
                            InSystem
                         ]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    icon = "icons/ship_parts/death-spore.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DARK_RAY"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/death-spore.png"

#include "../common/shared.macros"
