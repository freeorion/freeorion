Part
    name = "SP_BIOTERM"
    description = "SP_BIOTERM_DESC"
    class = Bombard
    capacity = 1.0
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]]
    buildtime = 3
    tags = [ "CTRL_BOMBARD_ORGANIC" ]
    location = All
    effectsgroups = [
        EffectsGroup    // players can order terminators used on enemies
            scope = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                HasTag name = "ORGANIC"
            ]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/ship_parts/bioterm.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_BIOTERM"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use terminators on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                HasTag name = "ORGANIC"
            ]
            activation = And [
                Unowned
                InSystem
            ]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/ship_parts/bioterm.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_BIOTERM"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/bioterm.png"

#include "/scripting/common/upkeep.macros"
