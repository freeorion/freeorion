Part
    name = "SP_VOID_SHADOW"
    description = "SP_VOID_SHADOW_DESC"
    class = Bombard
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]]
    buildtime = 3
    location = All
    effectsgroups = [
        EffectsGroup    // players can order void shadow used on enemies
            scope = And [
                Planet
                InSystem id = Source.SystemID
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                HasTag name = "PHOTOTROPHIC"
            ]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/ship_parts/bioterm.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_VOID_SHADOW"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use void shadow on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                HasTag name = "PHOTOTROPHIC"
            ]
            activation = And [
                            Unowned
                            InSystem
                         ]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/ship_parts/bioterm.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_VOID_SHADOW"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/bioterm.png"

#include "/scripting/common/upkeep.macros"
