Part
    name = "SP_DEATH_SPORE"
    description = "SP_DEATH_SPORE_DESC"
    class = Bombard
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]]
    buildtime = 3
    location = All
    effectsgroups = [
        EffectsGroup    // players can order death spores used on enemies
            scope = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                HasTag name = "ORGANIC"
            ]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    icon = "icons/ship_parts/death-spore.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DEATH_SPORE"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use death spores on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                HasTag name = "ORGANIC"
            ]
            activation = And [
                Unowned
                InSystem
            ]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    icon = "icons/ship_parts/death-spore.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DEATH_SPORE"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/death-spore.png"

#include "/scripting/common/upkeep.macros"
