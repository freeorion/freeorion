Part
    name = "SR_GRAV_PULSE"
    description = "SR_GRAV_PULSE_DESC"
    class = ShortRange
    damage = 1
    shots = 12
    destroyFightersPerBattleMax = ShipPartMeter part = "SR_GRAV_PULSE" meter = SecondaryStat object = Source.ID
    damageStructurePerBattleMax =
        max(0,ShipPartMeter part = "SR_GRAV_PULSE" meter = Capacity object = Source.ID - Value(Target.Shield))
        * ShipPartMeter part = "SR_GRAV_PULSE" meter = SecondaryStat object = Source.ID
    combatTargets = And [
        (CombatBout = 2)
        Or [
            Fighter
            [[COMBAT_TARGETS_NOT_DESTROYED_SHIP]]
            [[COMBAT_TARGETS_PLANET_WITH_DEFENSE]]
        ]
        [[COMBAT_TARGETS_VISIBLE_ENEMY]]
    ]
    mountableSlotTypes = [ Internal External ]
    buildcost = 20 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 1
    tags = [ "PEDIA_PC_DIRECT_WEAPON" ]
    location = And [
        Planet
        OwnedBy empire = Source.Owner
    ]
    icon = "icons/ship_parts/gravitic_pulse.png"

#include "shortrange.macros"
#include "/scripting/macros/upkeep.macros"
#include "/scripting/ship_parts/targeting.macros"
