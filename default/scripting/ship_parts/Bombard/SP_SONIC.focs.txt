Part
    name = "SP_SONIC"
    description = "SP_SONIC_DESC"
    class = Bombard
    capacity = 0.5
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 3
    tags = [ "CTRL_BOMBARD_LITHIC" "PEDIA_PC_BOMBARD" ]
    location = All
    effectsgroups = [
        EffectsGroup    // players can order sonic shockwave used on enemies
            scope = And [
                InSystem id = Source.SystemID
                Planet
                Species
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                /// @content_tag{LITHIC} When bombarding in-system enemy with this part, reduces population on visible populated Planet with tag
                HasTag name = "LITHIC"
            ]
            priority = [[POPULATION_DEFAULT_PRIORITY]]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_SONIC"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use sonic shockwave on player planets
            scope = NumberOf number = 1 condition = And [
                InSystem id = Source.SystemID
                Planet
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                /// @content_tag{LITHIC} Each part owned by Unowned in system will reduce population on one visible populated empire Planet with tag
                HasTag name = "LITHIC"
            ]
            activation = And [
                Unowned
                InSystem
            ]
            priority = [[POPULATION_DEFAULT_PRIORITY]]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_SONIC"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/sonic_wave.png"

#include "/scripting/macros/upkeep.macros"
#include "/scripting/macros/priorities.macros"
