Part
    name = "SP_EMO"
    description = "SP_EMO_DESC"
    class = Bombard
    capacity = 1.0
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 3
    tags = [ "CTRL_BOMBARD_ROBOTIC" "PEDIA_PC_BOMBARD" ]
    location = All
    effectsgroups = [
        EffectsGroup    // players can order EMO used on enemies
            scope = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OrderedBombardedBy condition = Source
                Or [
                    OwnedBy affiliation = EnemyOf empire = Source.Owner
                    Unowned
                ]
                VisibleToEmpire empire = Source.Owner
                /// @content_tag{ROBOTIC} When bombarding in-system enemy with this part, reduces population on visible populated Planet with tag
                HasTag name = "ROBOTIC"
            ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_2.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_EMO"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use EMO on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                /// @content_tag{ROBOTIC} Each part owned by Unowned in system will reduce population on one visible populated empire Planet with tag
                HasTag name = "ROBOTIC"
            ]
            activation = And [
                            Unowned
                            InSystem
                         ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_2.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_EMO"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/EMO.png"

#include "/scripting/common/upkeep.macros"
#include "/scripting/common/priorities.macros"
