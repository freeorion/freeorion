Special
    name = "TIDAL_LOCK_SPECIAL"
    description = "TIDAL_LOCK_SPECIAL_DESC"
    stealth = 0
    spawnrate = 0.2
    spawnlimit = 9999
    location = And [
        Planet
        Not Capital
        Not Planet type = [Asteroids GasGiant]
        Not Star type = [NoStar]
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Source
                Focus type = "FOCUS_INDUSTRY"
            ]
            effects = SetTargetIndustry value = Value + Target.Population
                        * (NamedReal name = "TIDAL_LOCK_TARGET_INDUSTRY_PERPOP"
                                     value = 1.0 * [[INDUSTRY_PER_POP]])

        EffectsGroup
            scope = Object id = Source.PlanetID
            activation = Species
            accountinglabel = "TIDAL_LOCK_LABEL"
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + Target.HabitableSize 
                        * (NamedReal name = "TIDAL_LOCK_TARGET_POPULATION_PERSIZE" value = -1)

        EffectsGroup
            scope = Source
            activation = Star type = [NoStar]
            effects = RemoveSpecial name = "TIDAL_LOCK_SPECIAL"

        [[SPECIAL_LIKES_OR_DISLIKES_SPECIAL_STABILITY_EFFECTS]]
    ]
    graphic = "icons/specials_huge/tidal_lock.png"

#include "/scripting/macros/priorities.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/specials/specials.macros.txt"
