Special
    name = "MODERATE_TECH_NATIVES_SPECIAL"
    description = "MODERATE_TECH_NATIVES_SPECIAL_DESC"
    stealth = 0
    spawnrate = 50
    spawnlimit = 9999
    location = And [
        Planet
        Unowned
        Species
        /// @content_tag{PRIMITIVE} Prevents this special from being added to a location with this tag during universe generation
        Not HasTag name = "PRIMITIVE"
        Not HasTag name = "NO_RESEARCH"
        Not HasTag name = "VERY_BAD_RESEARCH"
        Not HasSpecial name = "HIGH_TECH_NATIVES_SPECIAL"
    ]
    effectsgroups = [
        EffectsGroup
            scope = Source
            activation = Unowned
            effects = [
                SetMaxDefense value = Value + ( 10 * [[PLANET_DEFENSE_FACTOR]] )
                SetMaxShield value = Value + ( 10 * [[PLANET_SHIELD_FACTOR]] )
                SetMaxTroops value = Value + 10
                SetDetection value = Value + 20
            ]

        EffectsGroup
            scope = Source
            activation = Or [
                OwnedBy affiliation = AnyEmpire
                Not Species
            ]
            effects = RemoveSpecial name = "MODERATE_TECH_NATIVES_SPECIAL"
    ]
    graphic = "icons/tech/categories/learning.png"

#include "/scripting/macros/misc.macros"
#include "/scripting/macros/priorities.macros"