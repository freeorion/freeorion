Special
    name = "COMPUTRONIUM_SPECIAL"
    description = "COMPUTRONIUM_SPECIAL_DESC"
    stealth = 0
    spawnrate = 0.2
    spawnlimit = 9999
    location = And [
        Planet
        Not Planet type = [Asteroids GasGiant]
        Not ContainedBy And [
            System 
            Contains Capital
        ]
        Not WithinStarlaneJumps jumps = 2 condition = And [
            System
            Contains And [
                Planet
                OwnedBy affiliation = AnyEmpire
            ]
        ]
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Planet
                Focus type = "FOCUS_RESEARCH"
                OwnedBy empire = Source.Owner
                Happiness low = (NamedReal name = "COMPUTRONIUM_MIN_STABILITY" value = 18)
            ]
            activation = Focus type = "FOCUS_RESEARCH"
            stackinggroup = "COMPUTRONIUM_STACK"
            accountinglabel = "COMPUTRONIUM_ACCOUNTING_LABEL"
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects  = SetTargetResearch value = Value + Target.Population * (NamedReal name = "COMPUTRONIUM_TARGET_RESEARCH_PERPOP" value = 0.25 * [[RESEARCH_PER_POP]])

        [[CHANCE_OF_GUARD_2]]

        [[SPECIAL_LIKES_OR_DISLIKES_SPECIAL_STABILITY_EFFECTS]]
    ]
    graphic = "icons/specials_huge/computronium.png"

#include "monster_guard.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/macros/priorities.macros"
#include "/scripting/specials/specials.macros.txt"
