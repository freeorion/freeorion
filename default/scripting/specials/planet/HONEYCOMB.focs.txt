Special
    name = "HONEYCOMB_SPECIAL"
    description = "HONEYCOMB_SPECIAL_DESC"
    stealth = 0
    spawnrate = 9.0
    spawnlimit = 1
    location = And [
        Planet
        Not Planet type = Asteroids
        Not WithinStarlaneJumps jumps = 2 condition = And [
            System
            Contains And [
                Planet
                Or [
                    Species
                    HasSpecial
                ]
            ]
        ]
        Not WithinStarlaneJumps jumps = 5 condition = And [
            System
            Contains And [
                Planet
                OwnedBy affiliation = AnyEmpire
            ]
        ]
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Planet
                Not Species
                Not HasSpecial
                WithinStarlaneJumps jumps = 2 condition = Source
                Not ContainedBy Contains Or [
                    Design name = "SM_EXP_OUTPOST"
                    Building name = "BLD_EXPERIMENTOR_OUTPOST"
                ]
            ]
            activation = Turn high = 0
            stackinggroup = "GAME_START_MOD_STACK"
            priority = [[BEFORE_ANYTHING_ELSE_PRIORITY]]
            effects = [
                SetPlanetSize planetsize = Asteroids
                SetPlanetType type = Asteroids
            ]

        EffectsGroup
            scope = Source
            activation = Turn high = 0
            stackinggroup = "GAME_START_MOD_STACK"
            priority = [[BEFORE_ANYTHING_ELSE_PRIORITY]]
            effects = [
                SetPlanetSize planetsize = Huge
                SetPlanetType type = Barren
            ]

        EffectsGroup
            scope = And [
                Planet
                Focus type = "FOCUS_INDUSTRY"
                OwnedBy empire = Source.Owner
                ResourceSupplyConnected empire = Source.Owner condition = Source
            ]
            activation = Focus type = "FOCUS_INDUSTRY"
            stackinggroup = "HONEYCOMB_STACK"
            priority = [[TARGET_AFTER_2ND_SCALING_PRIORITY]]
            effects = SetTargetIndustry value = Value + Target.Population
                        * (NamedReal name = "HONEYCOMB_TARGET_INDUSTRY_PERPOP"
                                     value = 0.5 * [[INDUSTRY_PER_POP]])

        [[CHANCE_OF_GUARD_3]]
        
        [[SPECIAL_LIKES_OR_DISLIKES_SPECIAL_STABILITY_EFFECTS]]
    ]
    graphic = "icons/specials_huge/honeycomb.png"

#include "monster_guard.macros"
#include "/scripting/macros/base_prod.macros"
#include "/scripting/macros/priorities.macros"
#include "/scripting/specials/specials.macros.txt"