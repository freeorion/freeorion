Special
    name = "PHILOSOPHER_SPECIAL"
    description = "PHILOSOPHER_SPECIAL_DESC"
    stealth = 0
    spawnrate = 2.0
    spawnlimit = 1
    location = And [
        Planet
        Planet size = Tiny
        Not Planet type = [Asteroids GasGiant]
        Not Species
        ContainedBy And [
            System 
            Contains And [
                Planet
                Not Planet size = Tiny
            ]
        ]
        Not WithinStarlaneJumps jumps = 2 condition = And [
            System
            Contains And [
                Planet
                OwnedBy affiliation = AnyEmpire
            ]
        ]
    ]
    effectsgroups = [
        EffectsGroup
            scope = Source
            activation = Turn high = 0
            stackinggroup = "GAME_START_MOD_STACK"
            priority = [[BEFORE_ANYTHING_ELSE_PRIORITY]]
            effects = SetPlanetType type = Radiated

        EffectsGroup
            scope = And [
                Planet
                Not Population high = 0
                InSystem id = Source.SystemID
                Happiness low = (NamedReal name = "PHILOSOPHER_MIN_STABILITY" value = 10)
            ]
            activation = Unowned
            stackinggroup = "PHILOSOPHER_STACK"
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetTargetResearch value = Value
                        + (NamedReal name = "PHILOSOPHER_TARGET_RESEARCH_FLAT" value = 5)

        EffectsGroup
            scope = Source
            effects = SetTargetConstruction value = Value + (NamedReal name = "PHILOSOPHER_TARGET_CONSTRUCTION_FLAT" value = (-20))
            
        [[SPECIAL_LIKES_OR_DISLIKES_SPECIAL_STABILITY_EFFECTS]]
    ]
    graphic = "icons/specials_huge/philospher-planet.png"

#include "/scripting/common/priorities.macros"
#include "/scripting/specials/specials.macros.txt"
