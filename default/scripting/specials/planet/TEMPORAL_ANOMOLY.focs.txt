Special
    name = "TEMPORAL_ANOMALY_SPECIAL"
    description = "TEMPORAL_ANOMALY_SPECIAL_DESC"
    stealth = 0
    spawnrate = 0.2
    spawnlimit = 9999
    location = And [
        Planet
        Not Species
        Not Planet type = [Asteroids GasGiant]
    ]
    effectsgroups = [
        EffectsGroup
            scope = And [
                Source
                Focus type = "FOCUS_RESEARCH"
                Happiness low = (NamedReal name = "TEMPORAL_ANOMALY_MIN_STABILITY" value = 16)
            ]
            priority = [[TARGET_AFTER_SCALING_PRIORITY]]
            effects = SetTargetResearch value = Value + Target.Population
                          * (NamedReal name = "TEMPORAL_ANOMALY_TARGET_RESEARCH_PERPOP"
                                       value = 15 * [[RESEARCH_PER_POP]])

        EffectsGroup
            scope = Object id = Source.PlanetID
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + Target.HabitableSize
                          * (NamedReal name = "TEMPORAL_ANOMALY_TARGET_POPULATION_PERSIZE" value = (-5))
                          accountinglabel = "TEMPORAL_ANOMALY_SPECIAL"
                          
        [[SPECIAL_LIKES_OR_DISLIKES_SPECIAL_STABILITY_EFFECTS]]
    ]
    graphic = "icons/specials_huge/temporal_anomaly.png"

#include "/scripting/macros/base_prod.macros"
#include "/scripting/macros/priorities.macros"
#include "/scripting/specials/specials.macros.txt"

