Tech
    name = "SPY_PLANET_STEALTH_MOD"
    description = "SPY_PLANET_STEALTH_MOD_DESC"
    short_description = "GAME_MAKE_WORK_SHORT_DESC"
    category = "SPY_CATEGORY"
    researchcost = 1
    researchturns = 1
    Unresearchable
    tags = [ "PEDIA_SPY_CATEGORY" ]
    effectsgroups = [
        EffectsGroup
            scope = Or [
                Building
                Planet
            ]
            stackinggroup = "PLANET_STEALTH_MOD_STACK"
            accountinglabel = "BASIC_PLANET_STEALTH"
            effects = SetStealth value = Value + (NamedReal name = "PLANET_BASIC_STEALTH" value = 5.0)
        
        EffectsGroup
            scope = And [
                Ship
                InSystem
                OwnedBy empire = Source.Owner
            ]
            accountinglabel = "FLEET_UNSTEALTHINESS"
            effects = SetStealth value = Value - (NamedReal name = "FLEET_UNSTEALTH_SHIPS_SCALING" value = 5.0) *
                floor(
                    max(
                        0,
                        (Statistic Count condition = And [
                            Ship
                            InSystem id = Target.SystemID
                            OwnedBy empire = Source.Owner
                        ]) - (NamedReal name = "FLEET_UNSTEALTHY_SHIPS_THRESHOLD" value = 10)
                    ) ^ 0.5
                )
                // large fleets only start affecting stealth when there are more than the threshold of ships in a single system
    ]
    graphic = ""

#include "/scripting/common/base_prod.macros"
