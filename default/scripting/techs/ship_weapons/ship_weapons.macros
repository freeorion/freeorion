// @1@ part name
WEAPON_BASE_EFFECTS
'''        EffectsGroup
            scope = [[EMPIRE_OWNED_SHIP_WITH_PART]]
            accountinglabel = "@1@"
            effects = [
                SetMaxCapacity partname = "@1@" value = Value + PartCapacity name = "@1@"
                SetMaxSecondaryStat partname = "@1@" value = Value + PartSecondaryStat name = "@1@"
            ]

// The following is really only needed on the first resupplied turn after an upgrade is researched, since the resupply currently
// takes place in a portion of the turn before meters are updated, but currently there is no good way to restrict this to
// only that first resupply (and it is simply mildly inefficient to repeat the topup later).
           EffectsGroup
            scope = And [
                        [[EMPIRE_OWNED_SHIP_WITH_PART]]
                        Turn high = LocalCandidate.LastTurnResupplied 
            ]
            accountinglabel = "@1@"
            effects = SetCapacity partname = "@1@" value = Value + [[ARBITRARY_BIG_NUMBER_FOR_METER_TOPUP]]

'''

// @1@ part name
// @2@ value added to max capacity
WEAPON_UPGRADE_CAPACITY_EFFECTS
'''   [
        EffectsGroup
            scope = And [
                        [[EMPIRE_OWNED_SHIP_WITH_PART]]
                        [[SHIP_PART_UPGRADE_RESUPPLY_CHECK(CurrentContent)]]
            ]
            accountinglabel = "@1@"
            effects = SetMaxCapacity partname = "@1@" value = Value + ( @2@ * [[SHIP_WEAPON_DAMAGE_FACTOR]] )

// Inform the researching empire that ships in supply will get upgraded before next combat
        EffectsGroup
            scope = Source
            activation = (CurrentTurn == TurnTechResearched empire = Source.Owner name = CurrentContent)
            effects = GenerateSitRepMessage
              message = "SITREP_WEAPONS_UPGRADED"
              label = "SITREP_WEAPONS_UPGRADED_LABEL"
              icon = "icons/sitrep/weapon_upgrade.png"
              parameters = [
                  tag = "empire"    data = Source.Owner
                  tag = "shippart"  data = "@1@"
                  tag = "tech"   data = CurrentContent
                  tag = "dam"   data = @2@ * [[SHIP_WEAPON_DAMAGE_FACTOR]]
              ]
              empire = Target.Owner

      ]'''

#include "../techs.macros"

#include "/scripting/common/misc.macros"
