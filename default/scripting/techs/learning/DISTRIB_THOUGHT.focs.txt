Tech
    name = "LRN_DISTRIB_THOUGHT"
    description = "LRN_DISTRIB_THOUGHT_DESC"
    short_description = "RESEARCH_SHORT_DESC"
    category = "LEARNING_CATEGORY"
    researchcost = 75 * [[TECH_COST_MULTIPLIER]]
    researchturns = 5
    tags = [ "PEDIA_LEARNING_CATEGORY" ]
    prerequisites = "LRN_PSIONICS"
    effectsgroups = [
        EffectsGroup
            scope = And [
                ProductionCenter
                OwnedBy empire = Source.Owner
                Focus type = "FOCUS_RESEARCH"
                Happiness low = (NamedReal name = "LRN_DISTRIB_THOUGHT_MIN_STABILITY" value = 10)
            ]
            priority = [[TARGET_AFTER_2ND_SCALING_PRIORITY]]
            effects = SetTargetResearch value = Value +
                max(0.0, min(Target.Construction * (NamedReal name = "DISTRIB_THOUGH_INFRA_LIMIT_SCALING" value = 0.5),
                    (NamedReal name = "DISTRIB_THOUGH_RESEARCH_SCALING" value = 0.1) *
                    (Statistic Max
                        value = DirectDistanceBetween object = Target.ID object = LocalCandidate.ID
                        condition = And [
                            System
                            Contains And [
                                Planet
                                OwnedBy empire = Source.Owner
                                Focus type = "FOCUS_RESEARCH"
                            ]
                        ] ^ 0.5)))

        EffectsGroup
            scope = Source
            effects = SetEmpireMeter empire = Source.Owner
                                     meter = "MILITARY_CATEGORY_NUM_POLICY_SLOTS" value = Value + 1
    ]
    graphic = "icons/tech/distributed_thought.png"

#include "/scripting/common/base_prod.macros"
#include "/scripting/common/priorities.macros"
