BASIC_POPULATION
'''
        [[HOMEWORLD_BONUS_POPULATION]]
        [[ENVIRONMENT_MODIFIER]]
        [[SELF_SUSTAINING_BONUS]]
        [[PHOTOTROPHIC_BONUS]]
        [[GASEOUS_BONUS]]
        [[HOMEWORLD_GROWTH_FOCUS_BOOST]]


        // population growth or decay towards to target population
        EffectsGroup
            scope = Source
            activation = And [
                Planet
                (Source.LastTurnConquered < CurrentTurn)
            ]
            priority = [[EARLY_FIRST_NATURAL_POPULATION_PRIORITY]]
            effects = SetPopulation value =
                (Statistic If condition = (Value < Target.TargetPopulation) *
                    min(Target.TargetPopulation, Value + Value*0.01*(Target.TargetPopulation + 1 - Value)))
              + (Statistic If condition = (Value >= Target.TargetPopulation) *
                    max(Target.TargetPopulation, Value - 0.1*(Value - Target.TargetPopulation)))
            
        // Since all species have the same advanced focus effects and
        // infrastructure, the macros are stashed here were they don't
        // need to be manually included in each species macros.
        [[FOCUS_CHANGE_PENALTY]]
        [[ADVANCED_FOCUS_EFFECTS]]
        [[STANDARD_CONSTRUCTION]]
        [[STANDARD_METER_GROWTH]]
'''

AVERAGE_POPULATION
'''     [[BASIC_POPULATION]]
'''

BAD_POPULATION
'''     [[BASIC_POPULATION]]
    
        EffectsGroup
            description = "BAD_POPULATION_DESC"
            scope = Source
            activation = Planet
            priority = [[TARGET_POPULATION_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value -0.25*abs(Value) accountinglabel = "BAD_POPULATION_LABEL"
'''

GOOD_POPULATION
'''     [[BASIC_POPULATION]]

        EffectsGroup
            description = "GOOD_POPULATION_DESC"
            scope = Source
            activation = Planet
            priority = [[TARGET_POPULATION_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value +0.25*abs(Value) accountinglabel = "GOOD_POPULATION_LABEL"
'''

/*
* Implements environmental modifiers from Growth techs.
* Changes to the growth tree should take this into account.
*/
ENVIRONMENT_MODIFIER
'''     EffectsGroup
            scope = Source
            activation = And [
                Planet
                Planet environment = Uninhabitable
            ]
            priority = [[TARGET_POPULATION_OVERRIDE_PRIORITY]]
            effects = SetTargetPopulation value = -999 accountinglabel = "UNINHABTIABLE_ENVIRONMENT_LABEL"

        EffectsGroup
            scope = Source
            activation = And [
                Planet
                Planet environment = Hostile
            ]
            priority = [[TARGET_POPULATION_BEFORE_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value - 4 * Source.HabitableSize accountinglabel = "HOSTILE_ENVIRONMENT_LABEL"

        EffectsGroup
            scope = Source
            activation = And [
                Planet
                Planet environment = Poor
            ]
            priority = [[TARGET_POPULATION_BEFORE_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value - 2 * Source.HabitableSize accountinglabel = "POOR_ENVIRONMENT_LABEL"

/*        EffectsGroup
            scope = Source
            activation = And [
                Planet
                Planet environment = Adequate
            ]
            priority = [[TARGET_POPULATION_BEFORE_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 0 * Source.HabitableSize accountinglabel = "ADEQUATE_ENVIRONMENT_LABEL"
*/

        EffectsGroup
            scope = Source
            activation = Planet environment = Good
            priority = [[TARGET_POPULATION_BEFORE_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 3 * Target.HabitableSize accountinglabel = "GOOD_ENVIRONMENT_LABEL"
'''

HOMEWORLD_BONUS_POPULATION
'''     EffectsGroup
            scope = AND [
                Source
                Homeworld name = Source.Species
            ]
            activation = Planet
            stackinggroup = "HOMEWORLD_STACK"
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 2 * Target.HabitableSize accountinglabel = "HOMEWORLD_BONUS"
'''

HOMEWORLD_GROWTH_FOCUS_BOOST
'''     EffectsGroup 
            scope = And [
                Planet
                OwnedBy empire = Source.Owner
                Species name = Source.Species
                Not Homeworld name = Source.Species
                ResourceSupplyConnected empire = Source.Owner condition = Source
            ]
            activation = And [
                Planet
                Focus type = "FOCUS_GROWTH"
                Homeworld
            ]
            stackinggroup = "HOMEWORLD_STACK"
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 1 * Target.HabitableSize accountinglabel = "HOMEWORLD_SUPPLY"
'''

// This is dependent on current placement in population effects calc,
// just after Homeworld and Environment
SELF_SUSTAINING_BONUS
'''     EffectsGroup
            scope = Source
            activation = And [
                Planet
                HasTag name = "SELF_SUSTAINING"
            ]
            accountinglabel = "SELF_SUSTAINING_LABEL"
            priority = [[TARGET_POPULATION_AFTER_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value + 3 * Target.HabitableSize  // Gets the same bonus as three growth specials
'''

PHOTOTROPHIC_BONUS
'''     EffectsGroup
            scope = Contains Source
            activation = AND [
                Planet
                OR [
                    OwnedBy affiliation = AnyEmpire
                    Star type = [BlackHole NoStar] // Natives are allowed to have bright or dim stars
                ]
                HasTag name = "PHOTOTROPHIC"
                Turn high = 0
                NOT Star type = [Orange]
            ]
            effects = SetStarType type = Yellow // start with a normal star to be balanced
 
        EffectsGroup
            scope = Source
            activation = AND [
                Planet
                HasTag name = "PHOTOTROPHIC"
                Star type = [Blue]
                TargetPopulation low = 0
            ]
            accountinglabel = "VERY_BRIGHT_STAR"
            priority = [[TARGET_POPULATION_LAST_BEFORE_OVERRIDE_PRIORITY]]
            effects = SetTargetPopulation value = Value + 3 * Source.HabitableSize

        EffectsGroup
            scope = Source
            activation = AND [
                Planet
                HasTag name = "PHOTOTROPHIC"
                Star type = [White]
                TargetPopulation low = 0
            ]
            accountinglabel = "BRIGHT_STAR"
            priority = [[TARGET_POPULATION_LAST_BEFORE_OVERRIDE_PRIORITY]]
            effects = SetTargetPopulation value = Value + 1.5 * Source.HabitableSize

        EffectsGroup
            scope = Source
            activation = AND [
                Planet
                HasTag name = "PHOTOTROPHIC"
                Star type = [Red Neutron]
            ]
            accountinglabel = "DIM_STAR"
            priority = [[TARGET_POPULATION_LAST_BEFORE_OVERRIDE_PRIORITY]]
            effects = SetTargetPopulation value = Value - 1 * Source.HabitableSize

        EffectsGroup
            scope = Source
            activation = AND [
                Planet
                HasTag name = "PHOTOTROPHIC"
                Star type = [BlackHole NoStar]
            ]
            accountinglabel = "NO_STAR"
            priority = [[TARGET_POPULATION_LAST_BEFORE_OVERRIDE_PRIORITY]]
            effects = SetTargetPopulation value = Value - 10 * Source.HabitableSize
'''

GASEOUS_BONUS
'''     EffectsGroup
            scope = Source
            activation = And [
                Planet type = GasGiant
                HasTag name = "GASEOUS"
            ]
            accountinglabel = "GASEOUS_LABEL"
            priority = [[TARGET_POPULATION_SCALING_PRIORITY]]
            effects = SetTargetPopulation value = Value - 0.5*abs(Value)
'''

// @1@ Species key
LIFECYCLE_MANIP_POPULATION_EFFECTS
'''
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
            ]
            activation = And [
                Not OwnerHasTech name = "GRO_LIFECYCLE_MAN"
                Turn low = Source.CreationTurn + 1 high = Source.CreationTurn + 1
            ]
            effects = [
                SetSpecies name = @1@
                SetPopulation value = 1
            ]
            
        EffectsGroup
            scope = And [
                Object id = Source.PlanetID
                Planet
            ]
            activation = And [
                OwnerHasTech name = "GRO_LIFECYCLE_MAN"
                Turn low = Source.CreationTurn + 1 high = Source.CreationTurn + 1
            ]
            effects = [
                SetSpecies name = @1@
                SetPopulation value = [[MIN_RECOLONIZING_SIZE]]
            ]
'''

#include "/scripting/species/common/advanced_focus.macros"
#include "/scripting/species/common/focus.macros"
#include "/scripting/species/common/general.macros"
