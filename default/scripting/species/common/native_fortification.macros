FORTIFICATION_PROBABILITY
'''0.1 * max(0.2 * Source.TargetPopulation, Source.Research + Source.Industry)'''

ADVANCED_NATIVE_DEFENSE
'''[[AVERAGE_PLANETARY_SHIELDS]]
[[AVERAGE_PLANETARY_DEFENSE]]
[[NATIVE_PLANETARY_DETECTION(10)]]
[[NATIVE_PLANETARY_DEFENSE(10*[[PLANET_DEFENSE_FACTOR]])]]
[[NATIVE_PLANETARY_SHIELDS(10*[[PLANET_SHIELD_FACTOR]])]]

EffectsGroup
            scope = And [
                Source
                Turn low = 1 high = 1
                (GalaxyMaxAIAggression >= 1)
            ]
            activation = Planet
            effects = [
                If condition = Not ContainedBy Contains Or [
                        And [Planet HasSpecial name = "MODERATE_TECH_NATIVES_SPECIAL"]
                        And [Planet HasSpecial name = "HIGH_TECH_NATIVES_SPECIAL"]
                    ]
                    effects = AddSpecial name = OneOf("NATIVE_FORTIFICATION_LOW", "NATIVE_FORTIFICATION_MEDIUM", "CLOUD_COVER_MASTER_SPECIAL", "VOLCANIC_ASH_MASTER_SPECIAL")
            ]
'''

DEFAULT_NATIVE_DEFENSE
'''[[AVERAGE_PLANETARY_SHIELDS]]
[[AVERAGE_PLANETARY_DEFENSE]]

EffectsGroup
            scope = And [
                Source
                Turn low = 1 high = 1
                (GalaxyMaxAIAggression >= 1)
                ]
            activation = And [
                Planet
                Random probability = [[FORTIFICATION_PROBABILITY]]
                Not ContainedBy Contains Or [
                    And [Planet HasSpecial name = "MODERATE_TECH_NATIVES_SPECIAL"]
                    And [Planet HasSpecial name = "HIGH_TECH_NATIVES_SPECIAL"]
                    And [Species HasTag name = "PRIMITIVE"]
                    ]
            ]
            effects = [
                If condition = Not Random probability = [[FORTIFICATION_PROBABILITY]]
                    effects = [
                        AddSpecial name = OneOf("NATIVE_FORTIFICATION_MINIMAL", "NATIVE_FORTIFICATION_LOW", "CLOUD_COVER_MASTER_SPECIAL")
                    ]
                else = [
                    AddSpecial name = OneOf("NATIVE_FORTIFICATION_MEDIUM", "NATIVE_FORTIFICATION_HIGH", "VOLCANIC_ASH_MASTER_SPECIAL")
                ]
            ]
'''

#include "/scripting/species/common/detection.macros"
#include "/scripting/species/common/planet_defense.macros"
#include "/scripting/species/common/planet_shields.macros"
#include "/scripting/species/common/troops.macros"

#include "/scripting/common/misc.macros"

