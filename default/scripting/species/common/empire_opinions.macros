FIXED_OPINION_EFFECTS
'''
        EffectsGroup
            scope = Capital
            stackinggroup = "@1@_FIXED_OPINION_OF_EMPIRE"
            effects = [
                SetSpeciesTargetOpinion
                    species = ThisSpecies
                    empire = Target.Owner
                    opinion = @2@
                SetSpeciesOpinion
                    species = ThisSpecies
                    empire = Target.Owner
                    opinion = @2@
            ]
'''

COMMON_OPINION_EFFECTS
'''
        EffectsGroup
            scope = Capital
            stackinggroup = "@1@_TOWARDS_TARGET_OPINION_OF_EMPIRE"
            effects = [
                NoOp
                SetSpeciesTargetOpinion     // baseline opinion
                    species = ThisSpecies
                    empire = Target.Owner
                    opinion = Value + (GameRule name = "RULE_BASELINE_PLANET_STABILITY")
                        + ((GameRule name = "RULE_INVASION_OPINION_PENALTY_SCALING") *
                           (Statistic Count condition = And [
                                Planet
                                Species name = ThisSpecies
                                (LocalCandidate.LastInvadedByEmpire == Target.Owner)
                            ]
                           ) ^ 0.5)
                        + ((GameRule name = "RULE_SHIPS_LOST_DESTROYED_PENALTY_SCALING") *
                              ( SpeciesShipsDestroyed empire = Target.Owner name = ThisSpecies
                              + SpeciesShipsLost empire = Target.Owner name = ThisSpecies
                          ) ^ 0.5)

                SetSpeciesOpinion
                    species = ThisSpecies
                    empire = Target.Owner
                    opinion = Value + 
                        min(1.0,                // increase by 1 per turn up towards target
                            max(-1.0,           // decrease by 1 per turn down towards target
                                (SpeciesEmpireTargetOpinion
                                    species = ThisSpecies
                                    empire = Target.Owner
                                ) - Value
                            )
                        )
            ]
'''
