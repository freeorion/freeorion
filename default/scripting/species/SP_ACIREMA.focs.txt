Species
    name = "SP_ACIREMA"
    description = "SP_ACIREMA_DESC"
    gameplay_description = "SP_ACIREMA_GAMEPLAY_DESC"
    Native
    CanProduceShips

    tags = [ "SELF_SUSTAINING" "GREAT_INDUSTRY" "GOOD_RESEARCH" "GOOD_SHIELDS" "GREAT_SUPPLY" "PEDIA_SELF_SUSTAINING_SPECIES_CLASS" ]

    foci = [
        [[HAS_INDUSTRY_FOCUS]]
        [[HAS_RESEARCH_FOCUS]]
        [[HAS_GROWTH_FOCUS]]
        [[HAS_ADVANCED_FOCI]]
    ]

    effectsgroups = [
        [[GREAT_INDUSTRY]]
        [[GOOD_RESEARCH]]
        [[GREAT_STOCKPILE]]

        [[AVERAGE_POPULATION]]
        [[AVERAGE_HAPPINESS]]
        [[GREAT_SUPPLY]]
        [[AVERAGE_DEFENSE_TROOPS]]

        // not for description

        EffectsGroup
            scope = Source
            activation = AND [
                Planet
                OR [
                    Turn low = 10 high = 10
                    Turn low = 80 high = 80
                    Turn low = 150 high = 150
                    Turn low = 220 high = 220
                    Turn low = 290 high = 290
                ]
                Not WithinStarlaneJumps jumps = 1 condition = And [
                    System
                    Contains And [
                        Planet
                        OwnedBy affiliation = AnyEmpire
                    ]
                ]
            ]
            effects = CreateShip designname = "SM_ACIREMA_GUARD" species = "SP_ACIREMA"

        EffectsGroup
            scope = And [
                Object id = Source.SystemID
                System
            ]
            activation = Turn high = 0
            effects = SetStarType type = Blue

        [[GOOD_SHIP_SHIELDS]]

        EffectsGroup
            description = "GOOD_PLANETARY_DEFENSE_DESC"
            scope = And [
                Source
                Planet
            ]
            effects = SetMaxDefense value = Value + 5

        EffectsGroup
            description = "GOOD_PLANETARY_SHIELD_DESC"
            scope = And [
                Source
                Planet
            ]
            effects = SetMaxShield value = Value + 5

        [[AVERAGE_PLANETARY_SHIELDS]]
        [[AVERAGE_PLANETARY_DEFENSE]]

    ]

    [[RADIATED_STANDARD_EP]]

    graphic = "icons/species/acirema.png"

#include "common/*.macros"

#include "/scripting/common/*.macros"
