/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

layout mini_image_size
{
 interface:
    percent: false;
        
    view dialog (name: "Mini Image Size")
    {
        column()
        {
		   display_number(name: "Document Height:", bind: @original_height, format: "#",
		   		units: [{ name: "pixels", format: "#", scale: [ 1, 0 ] }]);
		   display_number(name: "Document Width:", bind: @original_width, format: "#",
		   		units: [{ name: "pixels", format: "#", scale: [ 1, 0 ] }]);
		   		
		   	row() { }
        
            overlay()
            {
                panel(bind: @percent, value: true)
                {
                    edit_number(name: "Height:", bind: @height_percent, digits: 7, format: "#.00");
                    edit_number(name: "Width:", bind: @width_percent, digits: 7, format: "#.00");
                }
                panel(bind: @percent, value: false)
                {
                    edit_number(name: "Height:", bind: @height_pixels, digits: 7, format: "#");
                    edit_number(name: "Width:", bind: @width_pixels, digits: 7, format: "#");
                }
            }
            checkbox(name: "Constrain Proportions", bind: @constrain);
            checkbox(name: "Percent", bind: @percent);
        }
        column()
        {
            button(name: "OK", bind: @result, action: @ok, default: true);
            button(name: "Cancel", action: @cancel);
        }
    }
}
