/*
    Copyright 2005-2007 Adobe Systems Incorporated
    Distributed under the MIT License (see accompanying file LICENSE_1_0_0.txt
    or a copy at http://stlab.adobe.com/licenses.html)
*/

layout mini_image_size
{
 interface:
 	original : false;
    view dialog (name: "Mini Image Size")
    {
        column()
        {
            edit_number(name: "Height:", digits: 7, 
                                    units: [    { name: "pixels", bind: @height_pixels, format: "#" },
                                                { name: "percent", bind: @height_percent, format: "#.00" }  ]);
                                                
            edit_number(name: "Width:", digits: 7, 
                                    units: [    { name: "pixels", bind: @width_pixels, format: "#" },
                                                { name: "percent", bind: @width_percent, format: "#.00" }  ]);

            checkbox(name: "Constrain Proportions", bind: @constrain);
			reveal(name: "Original Document", bind: @original);
			optional(bind: @original, horizontal: align_fill)
			{
			   display_number(name: "Document Height:", bind: @original_height, format: "#",
					units: [{ name: "pixels", format: "#", scale: [ 1, 0 ] }]);
			   display_number(name: "Document Width:", bind: @original_width, format: "#",
					units: [{ name: "pixels", format: "#", scale: [ 1, 0 ] }]);
			}
        }
        column()
        {
            button(name: "OK", action: @ok, default: true);
            button(name: "Cancel", action: @cancel);
        }
    }
}
